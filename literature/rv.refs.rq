SELECT DISTINCT ?item ?title ?pmid ?pmcid ?doi ?protLabel ?rna ?rev ?omics ?autoi ?asmbl ?repl ?top ?foll ?kerat ?entry ?uses
  (MIN(?pdate) AS ?pubdate)
{
  VALUES ?pw { wd:Q164778 }
  VALUES ?top { wd:Q7202 wd:Q128406 wd:Q908902 }
  ?item p:P921 [ ps:P921 ?pw].
  ?item p:P921 [ ps:P921 ?top].
  ?item wdt:P1476 ?title.
  ?item wdt:P577 ?pdate.
  OPTIONAL { 
    VALUES ?prot { wd:Q83133549 wd:Q24745872 wd:Q24770641 wd:Q81490100 wd:Q24774064 wd:Q24773176 wd:Q24724262 wd:Q24719751 wd:Q24785520 wd:Q24726417 wd:Q24739451 wd:Q24782928 }
    ?item wdt:P921 ?prot.
    BIND(
      IF(?prot = wd:Q83133549, 'VP1',
      IF(?prot = wd:Q24745872, 'VP2',
      IF(?prot = wd:Q24770641, 'VP3',
      IF(?prot = wd:Q81490100, 'VP4',
      IF(?prot = wd:Q24774064, 'VP6',
      IF(?prot = wd:Q24773176, 'VP7',
      IF(?prot = wd:Q24724262, 'NSP1',
      IF(?prot = wd:Q24719751, 'NSP2',
      IF(?prot = wd:Q24785520, 'NSP3',
      IF(?prot = wd:Q24726417, 'NSP4',
      IF(?prot = wd:Q24739451, 'NSP5',
      IF(?prot = wd:Q24782928, 'NSP6',
        'unknown'))))))))))))
      as ?protLabel)
  }
  OPTIONAL { 
    ?item wdt:P698 ?pmid.
  }
  OPTIONAL { 
    ?item wdt:P932 ?pmcid.
  }
  OPTIONAL { 
    ?item wdt:P356 ?doi.
  }
  OPTIONAL { 
    ?item ?rna wd:Q11053.
  }
  OPTIONAL { 
    ?item ?rev wd:Q7318358.
  }
  OPTIONAL { 
    VALUES ?omic { wd:Q471857 wd:Q28946449 wd:Q1098876 wd:Q106472333 wd:Q3589153 wd:Q105406038 }
    ?item ?omics ?omic.
  }
  OPTIONAL { 
    ?item ?autoi wd:Q8084905.
  }
  OPTIONAL { 
    ?item ?kerat wd:Q14864159.
  }
  OPTIONAL { 
    ?item ?asmbl wd:Q14916420.
  }
  OPTIONAL { 
    ?item ?repl wd:Q3933202.
  }
  OPTIONAL { 
    ?item ?entry wd:Q4118894.
  }
  OPTIONAL { 
    ?item wdt:P31 wd:Q580922.
    ?item wdt:P156 ?fitem.
    ?fitem wdt:P356 ?foll.
  }
  OPTIONAL { 
    ?item wdt:P4510 ?uses.
  }
}
GROUP BY ?item ?title ?pmid ?pmcid ?doi ?protLabel ?rna ?rev ?omics ?autoi ?asmbl ?repl ?top ?foll ?kerat ?entry ?uses
